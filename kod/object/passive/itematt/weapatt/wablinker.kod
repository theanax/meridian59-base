// Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
// All rights reserved.
//
// This software is distributed under a license that is described in
// the LICENSE file that accompanies it.
//
// Meridian is a registered trademark.


////////////////////////////////////////////////////////////////////////////////
WeapAttBlinker is WeaponAttribute

///////////////////////////////////////////////////////////////////////
//
//  This weapon has a chance to blind the opponent every time it does
//     damage.
//
//  Form is:
//
//      [ WA_BLINKER]
//
///////////////////////////////////////////////////////////////////////

constants:

   include blakston.khd

resources:

   WeapAttBlinker_desc = \
      " The name 'anax' is scribbled on the weapon with what looks like crayon."
   blinker_worked = "%s%s is sent away!"
   blinker_dm = "blinker"
   blinker_info_desc = "%i%% chance to cast Blink on hit\n"

classvars:

   vrDesc = WeapAttBlinker_desc
   vrInfoDesc = blinker_info_desc
   viItem_Att_Num = WA_blinker

   viDifficulty = 9
   vrDM_trigger = blinker_dm
   vbAllowSpecialAttributes = FALSE
   vbProcRequirement = TRUE

properties:

   // Modify the object's price by percent
   piValue_modifier = 200

   piValue_power_modifier = 10

   // Percent chance the effect procs
   piEffect_percent = 15
   
   piSpellPower = 100

messages:

   // Effect Functions

   ModifyDamage(damage=0,target=$,wielder=$,lData=$)
   {
      local oSpell;

      if target = $
         OR wielder = $
      {
         return damage;
      }

      if Random(1,100) < piEffect_percent
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_BLINK);

        Send(oSpell,@CastSpell,#who=target,#ltargets=[target],
                  #iSpellPower=piSpellPower,#bItemCast=TRUE);
        Send(wielder,@MsgSendUser,#message_rsc=blinker_worked,
                 #parm1=Send(target,@GetCapDef),#parm2=Send(target,@GetName),
                 #parm3=Send(target,@GetHisher));
         
      }

      return Damage;
   }

   // Add the spell name to the description.
   AppendDescInfo(oItem = $,compound = $, bShowAll = FALSE)
   {
      if (bShowAll
         OR Send(oItem,@GetIDStatusFromCompound,#compound=compound))
      {
         AddPacket(4,vrInfoDesc, 4,piEffect_percent);
      }
      else
      {
         // Not identified, add nothing (blank resource).
         AddPacket(4,system_blank_resource);
      }

      return;
   }

   AddToTreasureTable() // DOES NOT DROP...yet. *wink*
   {
      return;
   }
   
   IsMagicalEffect()
   {
      return TRUE;
   }

end
////////////////////////////////////////////////////////////////////////////////
